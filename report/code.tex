\subsection {Overview}

In this section, we give an overview of the overall structure of the code and the \texttt{PoGa} library.


Some parts of the \texttt{PoGa} library, such as the implementations of the games Tic-Tac-Toe and Hex, are not relevant and are thus not brought up here.
The same goes for the entire \texttt{Graphics} submodule, which implements support for a human player for the cases of Tic-Tac-Toe and Hex.

\subsection {Positional game library}

In this section, we introduce the \texttt{PoGa} library.
One part of \texttt{PoGa} is mainly an interface used for representing and playing positional games.
However, \texttt{PoGa} also provides the two strategies discussed earlier: minimax with alpha-beta pruning, and the UCT variant of MCTS.

\texttt{PoGa} also contains implementations of arbitrarily sized ($m \times n$) Tic-Tac-Toe and Hex.

\subsubsection {Game.hs}

This module defines the \texttt{Position} type-class, which is the central interface in the \texttt{PoGa} module.
It also defines the notion of a \texttt{Strategy} and how to play two strategies against each other in \texttt{playGame}.

Note that a strategy is just a move function, but that the result of the move is a monadic position value.
If we use the \texttt{IO} monad, we can represent a human player as a strategy. If we use a monad in the \texttt{MonadRandom} type-class, we can represent a random strategy. (Though, typically not completely random, of course.)

\begin{code}
module GameTheory.PoGa.Game
       (Player(First, Second),
        Game(..),
        Winner(..),
        Position(choices, winner, terminal, turn),
        playGame,
        playTournament,
        opponent)
        where

data Player = First | Second
  deriving (Eq, Show)
           
type Strategy m p = p -> m p

data Winner = Neither | Both | Only Player
  deriving (Eq, Show)

newtype Game p = Game {position :: p}

-- The typeclass Position provides an abstract
-- view of positional games
class Position p where
  choices :: p -> [p]
  winner :: p -> Winner
  terminal :: p -> Bool
  turn :: p -> Player

opponent :: Player -> Player
opponent First = Second
opponent Second = First

playGame :: (Position p, Monad m) =>
            Game p -> Strategy m p -> Strategy m p -> m Winner
playGame (Game pos) firstStrategy secondStrategy
  | terminal pos = return $ winner pos
  | otherwise = do
      pos' <- firstStrategy pos
      playGame (Game pos') secondStrategy firstStrategy
      
playTournament :: (Monad m, Position p) =>
                  Int -> Game p -> Strategy m p -> Strategy m p -> m [Winner]
playTournament n game fststrat sndstrat = do
  let g = playGame game fststrat sndstrat
  ws <- sequence $ replicate n g
  return ws
\end{code}

\subsubsection{SetGame.hs}

A typeclass works well as a general interface, but we need to have a concrete data-type to represent our games.

For many purposes, the following one works quite well. When it doesn't, a user of the library can always define his own data-type and use most of the functionality provided by the \texttt{PoGa} library, as long as the data-type is an instance of the \texttt{Position} typeclass, as described above.

\begin{code}
module GameTheory.PoGa.SetGame
       (SetGame(..), makeMove, fromWinningSets)
       where

import Data.Set as Set
import qualified GameTheory.PoGa.Game as G

data SetGame v = SetGame {
  board :: Set v,               -- all vertices of board
  turn :: G.Player,             -- who's turn is it?
  firstChoices :: Set v,        -- vertices that First occupy
  secondChoices :: Set v,       -- vertices that Second occupy
  firstWin :: Set v -> Bool,    -- is First winner?
  secondWin :: Set v -> Bool}   -- is Second winner?
                 
                 
instance Show v => Show (SetGame v) where
  show sg = show $ board sg

-- all unoccupied vertices
availableVertices :: Ord v => SetGame v -> Set v
availableVertices sg =  
  ((board sg) \\ (firstChoices sg)) \\ (secondChoices sg)

-- current player makes move corresponding to the given vertex
makeMove :: Ord v => SetGame v -> v -> SetGame v
makeMove sg vtx = 
  case turn sg of
    G.First -> sg{firstChoices = Set.insert vtx (firstChoices sg),
                turn = G.Second}
    G.Second -> sg{secondChoices = Set.insert vtx (secondChoices sg),
                 turn = G.First}

-- construct a game given a board and winning sets
fromWinningSets :: (Ord v) =>
  Set.Set v -> Set.Set (Set.Set v) -> Set.Set (Set.Set v) -> SetGame v
fromWinningSets board wssFirst wssSecond = 
  SetGame {board = board,
           turn = G.First,
           firstChoices = Set.empty,
           secondChoices = Set.empty,
           firstWin = win wssFirst,
           secondWin = win wssSecond}
  where
  -- Make a win function from a set of winning sets
  win :: (Ord v) => Set.Set (Set.Set v) -> Set.Set v -> Bool
  win wss s =
    or [ws `Set.isSubsetOf` s | ws <- Set.toList wss]


-- All possible moves that can be made for the given game.
instance Ord v => G.Position (SetGame v) where
  choices sg = 
    Set.toList $ Set.mapMonotonic (makeMove sg) (availableVertices sg)
  winner sg =
    case (firstWin sg $ firstChoices sg,
          secondWin sg $ secondChoices sg) of
      (True, False) -> G.Only G.First
      (False, True) -> G.Only G.Second
      (False, False) -> G.Neither
      (True, True) -> G.Both
  terminal sg =
    (Set.null $ availableVertices sg) || (G.winner sg /= G.Neither)
  turn sg = turn sg
\end{code}

\subsubsection {The \texttt{Strategy} module}

In this section, we list the strategy submodule of \texttt{PoGa}.
It contains implementations of minimax with alpha-beta pruning as well as UCT.
The implementations of these were discussed in less detail in sections \ref{sec:alpha_beta} and \ref{sec:uct}, respectively.

As mentioned, strategies take a position to a monadic position, where the monad typically is in \texttt{MonadRandom} or is the \texttt{IO} monad.

A strategy representing a human player would require some kind of user interface, which is not provided here.
It is provided in the module named \texttt{GraphicalGame}, and the required user interfaces are specified for the cases of arbitrarily sized Tic-Tac-Toe and Hex games.

Since it is not an important part of this paper, that entire module is left out of this section for space reasons.
However, the interested reader can download them from the repository (TODO: insert repository address here).

Below follows, \texttt{Perfect.hs}, the submodule containing the algorithm for perfect play, i.e. the minimax algorithm with alpha-beta pruning.

\begin{code}
module GameTheory.PoGa.Strategy.Perfect where

import GameTheory.PoGa.Game
import Data.List (find, sortBy, maximumBy, minimumBy)
import Data.Function (on)

perfectStrategyFirst :: (Monad m, Position p) => p -> m p
perfectStrategyFirst pos = return $ perfectStrategy First pos
perfectStrategySecond :: (Monad m, Position p) => p -> m p
perfectStrategySecond pos = return $ perfectStrategy Second pos

prunedMaximumBy :: (a -> a -> Ordering) -> (a -> Bool) -> [a] -> a
prunedMaximumBy compare isMax xs = 
  case find isMax xs of
    Just x -> x
    Nothing -> maximumBy compare xs

alternatingStrategy :: Position p =>
                       (Winner -> Winner -> Ordering) ->
                       Player -> p -> Winner
alternatingStrategy compareWinners player pos
  | terminal pos = winner pos
  | otherwise =
      let ws = map (alternatingStrategy
                    (flip compareWinners)
                    (opponent player)) (choices pos) in
      prunedMaximumBy compareWinners ((==) (Only player)) ws
    
perfectStrategy:: Position p => Player -> p -> p
perfectStrategy player pos = 
  let cs = choices pos
      ws = map (alternatingStrategy
                (flip compareWinners)
                (opponent player)) cs
      wcs = zip ws cs in
  snd $ prunedMaximumBy (compareWinners `on` fst) ((==) (Only player) . fst) wcs
  where
    winnerValue w
      | w == (Only player) = 1 
      | w == (Only $ opponent player) = -1 
      | otherwise = 0
    compareWinners w w' = compare (winnerValue w) (winnerValue w')
\end{code}

Next is \texttt{MCTS.hs}, containing our implementation of the UCT variant of MCTS.

\begin{code}
module GameTheory.PoGa.Strategy.MCTS
       (
         mctsStrategyFirst,
         mctsStrategySecond,
         unexploredMetaDataNode,
         MCTSNodeData (..)
        )
       where

import qualified GameTheory.PoGa.Game as Game
import qualified Control.Monad.Random as Random
import Test.QuickCheck
import Data.List (find, sortBy, maximumBy, minimumBy)
import Data.Maybe

data MetaDataNode a b p = MetaDataNode {
  firstData :: Maybe a,
  secondData :: Maybe b,
  choices :: [MetaDataNode a b p],
  terminal :: Bool,
  turn :: Game.Player,
  winner :: Game.Winner
  } deriving (Show)

data MCTSNodeData = MCTSNodeData {visitCount :: Int, score :: Score}
newtype MCTSNodeFirst b p = MCTSNodeFirst (MetaDataNode MCTSNodeData b p)
newtype MCTSNodeSecond a p = MCTSNodeSecond (MetaDataNode a MCTSNodeData p)

mctsStrategyFirst :: (Game.Position p, Random.MonadRandom m) =>
                     Int -> MetaDataNode MCTSNodeData b p ->
                     m (MetaDataNode MCTSNodeData b p)
mctsStrategyFirst numIterations node = do
  MCTSNodeFirst node' <- mctsStrategy numIterations (MCTSNodeFirst node)
  return node'
  
mctsStrategySecond :: (Game.Position p, Random.MonadRandom m) =>
                     Int -> MetaDataNode a MCTSNodeData p ->
                     m (MetaDataNode a MCTSNodeData p)
mctsStrategySecond numIterations node = do
  MCTSNodeSecond node' <- mctsStrategy numIterations (MCTSNodeSecond node)
  return node'

-- make an empty node with no metadata, given a node
unexploredMetaDataNode :: Game.Position p => p -> MetaDataNode a b p
unexploredMetaDataNode p =
  MetaDataNode {
    firstData = Nothing,
    secondData = Nothing,
    choices = map unexploredMetaDataNode $ Game.choices p,
    turn = Game.turn p,
    terminal = Game.terminal p,
    winner = Game.winner p
    }

class MCTSNode p where
  getMCTSData :: p -> Maybe MCTSNodeData
  setMCTSData :: p -> MCTSNodeData -> p
  setChoices :: p -> [p] -> p
  
explored :: MCTSNode p => p -> Bool
explored = isJust . getMCTSData
  
instance MCTSNode (MCTSNodeFirst b p) where
  getMCTSData (MCTSNodeFirst node) = firstData node
  setMCTSData (MCTSNodeFirst node) md = MCTSNodeFirst $ node {firstData = Just md}
  setChoices (MCTSNodeFirst node) cs =
    let unwrap (MCTSNodeFirst node) = node in
    MCTSNodeFirst $ node {choices = map unwrap cs}
    
instance MCTSNode (MCTSNodeSecond b p) where
  getMCTSData (MCTSNodeSecond node) = secondData node
  setMCTSData (MCTSNodeSecond node) md = MCTSNodeSecond $ node {secondData = Just md}
  setChoices (MCTSNodeSecond node) cs =
    let unwrap (MCTSNodeSecond node) = node in
    MCTSNodeSecond $ node {choices = map unwrap cs}

instance Game.Position p => Game.Position (MetaDataNode a b p) where
  choices mdn = choices mdn
  winner mdn = winner mdn
  terminal mdn = terminal mdn
  turn mdn = turn mdn
  
instance Game.Position p => Game.Position (MCTSNodeFirst b p) where
  choices (MCTSNodeFirst mdn) = map MCTSNodeFirst $ choices mdn
  winner (MCTSNodeFirst mdn) = winner mdn
  terminal (MCTSNodeFirst mdn) = terminal mdn
  turn (MCTSNodeFirst mdn) = turn mdn
  
instance Game.Position p => Game.Position (MCTSNodeSecond a p) where
  choices (MCTSNodeSecond mdn) = map MCTSNodeSecond $ choices mdn
  winner (MCTSNodeSecond mdn) = winner mdn
  terminal (MCTSNodeSecond mdn) = terminal mdn
  turn (MCTSNodeSecond mdn) = turn mdn

type Score = Double

compareChildren :: MCTSNode p => Double -> p -> p -> p -> Ordering
compareChildren cExp node a b =
  case (getMCTSData a, getMCTSData b) of
    (Just aData, Just bData) ->
      let Just parentData = getMCTSData node in
      compare (reconScore parentData aData) (reconScore parentData bData)    
      where
        reconScore :: MCTSNodeData -> MCTSNodeData -> Score
        reconScore parentData childData =
          let (vcp, sp) = (visitCount $ parentData, score $ parentData)
              (vcc, sc) = (visitCount $ childData, score $ childData) in
          ( sc / (fromIntegral vcc) ) +
          cExp * sqrt (  2.0*(log $ fromIntegral vcp) / (fromIntegral vcc)  )
    (Nothing, Nothing) -> EQ
    (Just _, Nothing) -> LT
    (Nothing, Just _) -> GT

-- TODO: rewrite as map best child
-- Also need MCTS nodes to store children in a list
popBestChild :: (MCTSNode p, Game.Position p) => Double -> p -> (p, [p])
popBestChild cExp node
  | explored node = popMaximumBy (compareChildren cExp node) (Game.choices node)
  | otherwise = error "popBestChild: un-explored node given"

findBestMove :: (MCTSNode p, Game.Position p) => p -> p
findBestMove node =
  case filter explored (Game.choices node) of
    [] ->
      error "cannot find a best move: no children are explored"
    explored ->
      maximumBy (compareChildren 0.0 node) explored

mctsStrategy :: (MCTSNode p, Game.Position p, Random.MonadRandom m) =>
                Int -> p -> m p
mctsStrategy 0 node = do
  return $ findBestMove node
mctsStrategy numSteps node = do
  (_, node') <- explore cExp node
  mctsStrategy (numSteps-1) node'
  where
  cExp :: Double  
  cExp = 1.0 / (sqrt 2.0) -- amount of exploration
  
explore :: (MCTSNode p, Game.Position p, Random.MonadRandom m) =>
           Score -> p -> m (Score, p)
explore cExp node = 
  case getMCTSData node of
    Nothing -> do -- not explored
      s <- if Game.terminal node then return $ value node else recon node
      return (s, setMCTSData node $ MCTSNodeData {visitCount = 1, score = s})
    Just (MCTSNodeData {visitCount = vc, score = sc}) -> do
      case Game.terminal node of
        True -> do
          let s = value node
          return (s, setMCTSData node $ MCTSNodeData {visitCount = vc + 1,
                                                      score = sc + s})            
        False -> do
          let (c, cs) = popBestChild cExp node
          (s, c') <- explore cExp c
          -- score is negated since it is the score of a
          -- child node relative to this node          
          let s' = negate s
              node' = setChoices node (c':cs) in 
            return (s', setMCTSData node' $ MCTSNodeData {visitCount = vc + 1,
                                                          score = sc + s'})
  
recon :: (Random.MonadRandom m, MCTSNode p, Game.Position p) => p -> m Score
recon pos
  | Game.terminal pos = return $ value pos
  | otherwise = do
      c <- Random.fromList [(c,1) | c <- Game.choices pos]
      s <- recon c
      -- note that score changes sign, since this is the childs score
      return $ -s

-- helper function: takes a non-empty list and
-- extracts it's maximum
popMaximumBy :: (a -> a -> Ordering) -> [a] -> (a, [a])
popMaximumBy _ [] = error "popMaximumBy: empty list"
popMaximumBy _ [x] = (x, [])
popMaximumBy cmp (x:xs) = 
  let (m, xs') = popMaximumBy cmp xs in
  if cmp m x == LT then (x, m:xs') else (m, x:xs')

value :: (Game.Position p, MCTSNode p) => p -> Score
value pos
  | Game.turn pos == Game.First = valueFirst pos
  | otherwise = negate $ valueFirst pos

valueFirst :: (Game.Position p, MCTSNode p) => p -> Score
valueFirst node =
  case Game.winner node of
    Game.Neither -> 0.0
    Game.Both -> 0.0
    Game.Only Game.First -> -1.0
    Game.Only Game.Second -> 1.0
\end{code}

The \texttt{Strategy} submodule also contains \texttt{Random.hs}, a strategy for a completely random player.
